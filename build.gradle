plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'maven-publish'
}

group = 'com.doubledeltas'
version = '1.5.3-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

def targetMinecraftVersion = "1.21.5"

dependencies {
    implementation 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    compileOnly "org.spigotmc:spigot-api:$targetMinecraftVersion-R0.1-SNAPSHOT"

    implementation 'io.ktor:ktor-server-core:2.3.6'
    implementation 'io.ktor:ktor-server-netty:2.3.6'
    implementation 'io.ktor:ktor-server-content-negotiation:2.3.6'
    implementation 'io.ktor:ktor-serialization-gson:2.3.6'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    withSourcesJar()
}

kotlin {
    jvmToolchain {
        languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks {
    runServer {
        minecraftVersion(targetMinecraftVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    repositories {
        mavenLocal()
    }

    publications {
        register("mavenJava", MavenPublication) {
            from components.java

            pom {
                name = project.name
                url = "https://github.com/LUDDANG/MineCollector"
                description = "MineCollector Plugin"

                licenses {
                    license {
                        name = "UNLICENSED"
                    }
                }

                developers {
                    developer {
                        id = "DoubleDeltas"
                        name = "DoubleDeltas"
                        email = "thesmartist@naver.com"
                    }
                    developer {
                        id = "return0927"
                        name = "Eunhak Lee"
                        email = "lee@enak.kr"
                    }
                }

                scm {
                    url = "https://github.com/LUDDANG/MineCollector"
                }
            }
        }
    }
}
